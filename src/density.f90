
subroutine density


! ******************************************************
! ***   FIND DENSITY AND CURRENT IN PHYSICAL SPACE   ***
! ******************************************************
!
! This subroutine integrates over momentum space
! to find rho and curr.  These quantities are
! defined as:
!
!                /
! rho  =  1/r**2 | f dp 
!                /
!
!                /
! curr =  1/r**2 | p f dp 
!                /
!

! Include modules.

  use parameters
  use arrays
  use functions
  use utils
! Declare variables.

  implicit none

  integer i,j
  real(8) :: smallpi,factor,average_rho,mass

  character(20) filename ! Name of outupt file.


  smallpi = acos(-1.0d0)


! Zero Angular Momentum
  if (Lfix == 0.0d0) then

     factor = 1.0d0

! Include Angular Momentum
  else

     factor = 2.0*smallpi*Lfix*drc*dpc!0.25D0/smallpi*drc*dpc

  endif  

  rho = 0.D0
  avg_rho = 0.D0
  curr = 0.D0

! NOTE: parallelize only over "i" (not collapse(2) over i and j).
! rho(i)/curr(i)/avg_rho(i) are accumulated across all j for a given
! i, so collapsing i and j together lets different threads update
! the same i concurrently with no atomic/reduction protection -- a
! data race.  Keeping the parallel loop over i alone means each i is
! owned by exactly one thread for the whole inner j loop, which is
! race-free without needing atomics.
  !$OMP PARALLEL DO SCHEDULE(GUIDED) PRIVATE(j)

  do i=1,Nr
    do j=1,Npart
      if (abs(r(i)-r_part(j))<=(bsplineorder+1)*drc) then

        rho(i) = rho(i) + f(j)*Sn(bsplineorder,(r(i)-r_part(j))/drc,drc)
        curr(i) = curr(i)+f(j)*p_part(j)*Sn(bsplineorder,(r(i)-r_part(j))/drc,drc)
      end if

      if (abs(r(i)-r_part(j))<=(bsplineorder+1)*dr) then

        avg_rho(i) = avg_rho(i) + f(j)/(dr+dr**3/(12.d0*r(i)**2))*Wn(bsplineorder,(r(i)-r_part(j))/dr)

      end if

    end do
  end do
  !$OMP END PARALLEL DO

! Ghost points using the reflection symmetry f(r,p) = f(-r,-p),
! which for scalars integrated over p (rho, avg_rho) is even:
! rho(-r) = rho(r).  The grid is staggered by dr/2 to avoid the
! r=0 singularity (r(i) = (i-0.5)*dr), so the ghost point with
! index (1-k) sits at -r(k) and must mirror the physical point k,
! i.e. rho(1-k) = rho(k) (NOT rho(k-1) = rho(k), which instead
! overwrites the physical point at index (k-1) with the value at
! k, corrupting rho near the origin).
  do i=1,ghost
      rho(1-i) = rho(i)
      avg_rho(1-i) = avg_rho(i)
  end do


  rho = factor*m0*rho/r**2
  avg_rho = factor*m0*avg_rho/r**2

  average_rho = 0.D0

! Integrate with the trapezoidal rule. Second order accurate.

!  do i=1,Nr
    do j=1,Npart
!      if (r1<=r(i) .and. r(i)<=r2) then
      if (r_part(j)>=r1 .and. r_part(j)<= r2) then
      
 !       average_rho = average_rho + 1.0D0/(r2**2-r1**2)*0.5D0*(avg_rho(i)*r(i)**2+avg_rho(i+1)*r(i+1)**2)*dr
        average_rho = average_rho + 1.0D0/(r2**2-r1**2)*f(j)*0.25D0/smallpi

      end if
    end do
!  end do

  filename = 'vlasov_rhomix'
  call save0Ddata(directory,filename,t,average_rho)


end subroutine density



subroutine avg_density

! In order to make the Poisson subroutine more efficient, 
! I will separate the subroutine that calculates 
! the average density from the rest of integrals.

! ******************************************************
! ***   FIND DENSITY AND CURRENT IN PHYSICAL SPACE   ***
! ******************************************************
!
! This subroutine integrates over momentum space
! to find only rho which is defined as:
!                /
! rho  =  1/r**2 | f dp 
!                /

! Include modules.

  use parameters
  use arrays
  use functions
  use utils
! Declare variables.

  implicit none

  integer i,j
  real(8) :: smallpi,factor,diff,contribution

  smallpi = acos(-1.0d0)


! Zero Angular Momentum
  if (Lfix == 0.0d0) then

     factor = 1.0d0

! Include Angular Momentum
  else

     factor = 2.0*smallpi*Lfix*drc*dpc!0.25D0/smallpi*drc*dpc

  endif  

  avg_rho = 0.D0

  !!$OMP PARALLEL DO SCHEDULE(GUIDED) private (j) collapse(2)
!!$OMP PARALLEL DO SCHEDULE(GUIDED) collapse(2)

!  do i=1,Nr
!    do j=1,Npart
!      if (abs(r(i)-r_part(j))<=(bsplineorder+1)*drc) then


!        avg_rho(i) = avg_rho(i) + f(j)/drc*Wn(bsplineorder,(r(i)-r_part(j))/drc)


!      end if
!    end do
!  end do
!  !$OMP END PARALLEL DO

!  !$OMP PARALLEL DO 
!$OMP PARALLEL DO SCHEDULE(GUIDED) SHARED(avg_rho, r, r_part, f, bsplineorder, drc) PRIVATE(i, j, diff, contribution)

  do i = 1, Nr
    do j = 1, Npart
        diff = abs(r(i) - r_part(j))
        if (diff <= (bsplineorder + 1) * dr) then
            contribution = f(j) / (r(i)**2*dr+dr**3/12.d0) * Wn(bsplineorder, diff / dr)
           !$OMP ATOMIC
            avg_rho(i) = avg_rho(i) + contribution
        end if
    end do
  end do
  !$OMP END PARALLEL DO


! Ghost points using the reflection symmetry avg_rho(-r) = avg_rho(r)
! (see the matching note in subroutine density above).
  do i=1,ghost
      avg_rho(1-i) = avg_rho(i)
  end do

  avg_rho = factor*m0*avg_rho

end subroutine avg_density
